/*
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

namespace org.accordproject.payment.uponsignature

import org.accordproject.cicero.runtime.*
import org.accordproject.signature.ContractSigned
import org.accordproject.payment.PaymentReceived

/**
 * This smart clause emits a payment obligation when the contract is signed
 */
contract PaymentUponSignature over PaymentUponSignatureContract state PaymentUponSignatureState {
  
	/**
	 * Sets the initial state for the clause
	 */
  clause init(request : Request) : Response {		
		set state PaymentUponSignatureState {
			stateId: "1",
			status: INITIALIZED
		};
    return Response{}
  }

	/**
	 * When the contract is signed a payment obligation is emitted
	 */
  clause contractSigned(request : ContractSigned) : Response emits PaymentObligation {
	  enforce (state.status = INITIALIZED) 
	    else throw ErgoErrorResponse{ message: `Contract state is invalid {{% state.status %}}` };
		
    emit PaymentObligation{
			contract: contract,
			promisor: some(contract.buyer),
			promisee: some(contract.seller),
			deadline: none,
			amount: contract.amount,
			description: contract.buyer.partyId ++ " should pay contract amount to " ++ contract.seller.partyId
		};
		set state PaymentUponSignatureState {
			stateId: "1",
			status: OBLIGATION_EMITTED
		};
    return Response{}
  }

  /**
	 * Complete the contract when payment is received.
	 */
  clause paymentReceived(request : PaymentReceived): Response {
    enforce (state.status = OBLIGATION_EMITTED) 
    else throw ErgoErrorResponse{ message: "Either a payment obligation hasn't yet been emitted by the contract or payment notification has already been received" };
		set state PaymentUponSignatureState {
			stateId: state.stateId,
			status: COMPLETED,
		};
		return Response{}
  }
}